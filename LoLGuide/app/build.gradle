buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}


android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        applicationId "com.carlospinan.champions"
        minSdkVersion 16
        targetSdkVersion 23
        versionCode 2
        versionName "1.0.1"
        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    buildTypes.each {
        it.buildConfigField 'String', 'LOL_API_KEY', lolDevApiKey
        it.buildConfigField 'String', 'LOL_STATIC_DATA_ENDPOINT', lolStaticDataEndpoint
    }
    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }
    signingConfigs {
        release {
            def Properties localProps = new Properties()
            localProps.load(new FileInputStream(file('../local.properties')))
            storeFile file("../debugKeystore/debugKeystore.jks")
            storePassword localProps["storePass"]
            keyAlias localProps["releaseKeystoreAlias"]
            keyPassword localProps["releaseKeystorePassword"]
        }
        debug {
            storeFile file("../debugKeystore/debugKeystore.jks")
            storePassword "123456"
            keyAlias "debugapi"
            keyPassword "123456"
        }
    }
}

ext {
    supportLibraryVersion = '23.1.1'
    espressoVersion = '2.2.1'
    runnerVersion = '0.3'
    googleServicesVersion = '8.4.0'
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:' + supportLibraryVersion
    compile 'com.android.support:design:' + supportLibraryVersion
    compile 'com.android.support:recyclerview-v7:' + supportLibraryVersion
    compile 'com.android.support:cardview-v7:' + supportLibraryVersion
    compile 'com.android.support:support-v4:' + supportLibraryVersion
    compile 'com.google.android.gms:play-services-ads:' + googleServicesVersion
    compile 'com.google.android.gms:play-services-analytics:' + googleServicesVersion

    compile 'com.github.bumptech.glide:glide:3.6.1'
    compile 'com.github.florent37:glidepalette:1.0.6'

    compile 'com.squareup.retrofit:retrofit:2.0.0-beta1'
    compile 'com.squareup.retrofit:converter-gson:2.0.0-beta1'
    compile 'com.squareup.okhttp:logging-interceptor:2.6.0'

    compile 'com.jakewharton:butterknife:7.0.1'
    compile 'io.realm:realm-android:0.87.4'

    // Android Testing Support Library's runner and rules
    androidTestCompile "com.android.support.test:runner:" + runnerVersion
    androidTestCompile "com.android.support.test:rules:" + runnerVersion

    compile "com.android.support.test.espresso:espresso-idling-resource:" + espressoVersion
    androidTestCompile 'com.android.support:support-annotations:' + supportLibraryVersion
    androidTestCompile "com.android.support.test.espresso:espresso-core:" + espressoVersion
    androidTestCompile "com.android.support.test.espresso:espresso-contrib:" + espressoVersion
    androidTestCompile "com.android.support.test.espresso:espresso-intents:" + espressoVersion

    compile('com.crashlytics.sdk.android:crashlytics:2.5.5@aar') {
        transitive = true;
    }

}

configurations.all {
    resolutionStrategy.force "com.android.support:support-annotations:" + supportLibraryVersion
    resolutionStrategy.force "com.android.support:recyclerview-v7:" + supportLibraryVersion
    resolutionStrategy.force "com.android.support:support-v4:" + supportLibraryVersion
}
